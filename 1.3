/*******PHASE 1.3*******
 * 
 * This is an on going project.
 * You are welcome to use it and share it.
 * 
 * Updates:
 * - best lap,
 * - current lap,
 * - code logistic.
 * (Big thanks to Arduino community)
 *
 * Requirements:
 * - Arduino uno,
 * - momentary switches,(pins: 8, 9, 10, 11, GRND)
 * - lcd display,(regular I2C pins: A5, A4, VCC, GRND)
 * - breadboard,
 * - and jump wires.
 * 
*/
#include <LiquidCrystal_I2C.h>
#include <Wire.h>
LiquidCrystal_I2C lcd(0x3F,20,4);

//This array arrangement has changed a little bit
//to allow us to change the number of lanes in use.
const byte laneCount = 4;
const char *placeNames[] = {"P1", "P2", "P3", "P4"};
const byte laneSensorPins[] = {8, 9, 10, 11};

// Global variables default to zero
boolean lanePreviousState[laneCount];
int lapCounter[laneCount];
unsigned long timeOfLastLap[laneCount];
int lanePosition[laneCount];

//****NEW CODE******
//Timing variables
int lanePeriod[laneCount];
unsigned long timeNow[laneCount];
unsigned long carTimeLap[laneCount];
unsigned long carStart[laneCount];
unsigned long carBest[laneCount];
float lastLap[laneCount];
float bestLap[laneCount];
boolean firstLap[laneCount];
boolean newBest[laneCount];
//******************

void setup()
{
  lcd.init();
  lcd.backlight();
  lcd.clear();
  lcd.setCursor(0,0);                 //This displays a welcome message
  lcd.print("********************");  //
  lcd.setCursor(1,1);                 //
  lcd.print("Johnny's  Workshop");    //
  lcd.setCursor(0,2);                 //
  lcd.print("Slot Car Lap Counter");  //
  lcd.setCursor(0,3);                 //
  lcd.print("********************");  //*******************************
  delay(2000);  //This delay() and clear() are only useful
  lcd.clear();  //if you wish to display a welcome message.

  for (int lane = 0; lane < laneCount; lane++)
  {
    pinMode(laneSensorPins[lane], INPUT_PULLUP);
//***********NEW CODE*****************
//This will set the timing variables
    carStart[lane] = millis();
    carBest[lane] = 9999;
    lastLap[lane] = 00.00;
    bestLap[lane] = 00.00;
    firstLap[lane] = true;
    newBest[lane] = false;
//************************************
  }
}
void loop()
{
  boolean newData = false;  // Set when a lap crossing happens

  for (int lane = 0; lane < laneCount; lane++)
  {
    boolean laneState = digitalRead(laneSensorPins[lane]) == LOW;

    if (laneState != lanePreviousState[lane])
    {
      lanePreviousState[lane] = laneState;

      if (laneState)  // Just went active
      {
        lapCounter[lane]++;
        timeOfLastLap[lane] = millis();
        newData = true;
        lcd.setCursor(0, lane);
        lcd.print(lapCounter[lane]);

//********NEW CODE*******************************************************
//The timing method came from an older project,
//it's been modified to suit the current layout.
//***********************************************************************
        timeNow[lane] = millis();
        carTimeLap[lane] = millis() - carStart[lane];
        carStart[lane] = millis();
        lastLap[lane] = carTimeLap[lane] / 1000.00;
        if (firstLap[lane] == true)
        {
          lcd.setCursor(10,lane);
          lcd.print("-");
          lcd.setCursor(5,lane);
          lcd.print("HOT");
          lcd.setCursor(12,lane);
          lcd.print("LAP");
        }
        if (firstLap[lane] != true){
          lcd.setCursor(4,lane);
          lcd.print("      ");
          lcd.setCursor(4,lane);
          lcd.print(lastLap[lane]);
        }
        if (carTimeLap[lane] < carBest[lane] && firstLap[lane] != true){
          carBest[lane] = carTimeLap[lane];
          bestLap[lane] = lastLap[lane];
          lcd.setCursor(12,lane);
          lcd.print("      ");
          lcd.setCursor(12,lane);
          lcd.print(bestLap[lane]);
          newBest[lane] = true;
        }   
        firstLap[lane] = false;
        if (newBest[lane] == true)newBest[lane] = false;
//**************************************************************************
      }
    }
  }
  // If nobody finished a lap, we're done
  if (!newData)
    return;

  // Determine the positions.
  // Compare each lane with the other lanes to count how many are ahead of them
  for (int lane = 0; lane < laneCount; lane++)
  {
    int aheadOfMe = 0;
    for (int otherLane = 0; otherLane < laneCount; otherLane++)
    {
      // Skip comparing a lane to itself
      if (lane == otherLane)
        continue;

      // If they have gone more laps, they are ahead of me
      if (lapCounter[lane] < lapCounter[otherLane])
      {
        aheadOfMe++;
        continue;
      }
      else
        // If they have gone the same number of laps but
        // they got there first, they're ahead of me.
        if (lapCounter[lane] == lapCounter[otherLane] &&
            timeOfLastLap[lane] > timeOfLastLap[otherLane])
        {
          aheadOfMe++;
          continue;
        }
    }

    // Compared against all other lanes so 'aheadOfMe gives my position
    lanePosition[lane] = aheadOfMe;

    // Display the position for this lane
    lcd.setCursor(18, lane);
    lcd.print(placeNames[lanePosition[lane]]);
  }
}
